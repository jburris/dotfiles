#!/bin/zsh
#
# Let's get some prompt action onnnn
#
# Setup PS1

autoload -Uz add-zsh-hook

# evaluate functions inside $PS1
setopt prompt_subst

# show rbenv info
if [[ $jb_rbenv_present == 1 ]]; then
    jb_rbenv_prompt() {
        echo -n " $jb_rbenv_ps1"
    }

    jb_update_rbenv_prompt() {
        local jb_rbenv_info
        jb_rbenv_info=`rbenv version | cut -f 1 -d \ `@[`rbenv gemset active 2>&/dev/null`]
        jb_rbenv_ps1="%Brbenv:%b$jb_rbenv_info"
    }

    add-zsh-hook chpwd jb_update_rbenv_prompt
fi

function jb_prompt_init() {
    local jb_ps_info
    jb_ps_info=''

    # Hostname and pwd
    jb_ps_info+="%B%m%b:%~"

    # Git status info for pwd
    jb_ps_info+='$(jb_git_ps1)'

    # rbenv current ruby@gemset
    [[ $jb_rbenv_present == 1 ]] && jb_ps_info+='$(jb_rbenv_prompt)'

    # Number of background jobs (if any)
    jb_ps_info+="%(1j: [+%j]:)"

    # Exit status of last job (if non-zero)
    jb_ps_info+="%(?.. [%?])"

    # The multiline prmopt
    PS1="$jb_ps_info
%B%#%b "

}

# From Robert's dotto code.
# https://github.com/rsanders/dotto/raw/stable/zsh/functions/display_dimensions
function display_dimensions() {
  emulate -L zsh

  local string="$1" width=${2:-$COLUMNS}
  typeset -L -i 10 lines=0

  local -E divisor=0.0
  local -E split=0.0 rem=0.0

  (( divisor = ${COLUMNS} + 0.0 ))

  for line in "${(f)string}"; do
      (( split = $#line / $divisor ))
      (( rem = $#line % $divisor ))
      (( lines = lines + $split ))
      if [[ "$rem" -gt 0 ]]; then
          (( lines = lines + 1 ))
      fi
  done

  REPLY=$lines
  printf "%d\n" "$lines"
}

# Also from Robert's dotto code. Replaced echotc with terminfo.
# https://github.com/rsanders/dotto/raw/stable/zsh/themes/prompt_rsbox_setup
function prompt_rsbox_eraseprompt() {
    emulate -L zsh

    local typed="$1"
    local i upcount

    local visible="# ${typed}"
    local visiblesize=""

    display_dimensions "$visible" > /dev/null && visiblesize=$REPLY

    (( upcount = 1 + ${visiblesize} ))

    local up=$terminfo[cuu1] el=$terminfo[el] out=""
    if [ $upcount -lt $LINES ]; then
        for (( i=0; i < $upcount; i++ )); do
            out+="${up}${el}"
        done
        echo -n -E "${out}${el}"
    fi
}

function jb_prompt_precmd() {
    if [[ $jb_preexec_ran == 0 ]]; then
	# From Robert's rsbox prompt
        # must be an empty cmd line; just clear and redraw prompt one line down
	prompt_rsbox_eraseprompt
	print -P "$cb[green]%# $cb[none]"
    fi

    jb_preexec_ran=0
}

if [[ $TERM = dumb ]]; then
    PS1='%# '
else
    jb_prompt_init

		add-zsh-hook precmd jb_prompt_precmd
fi
