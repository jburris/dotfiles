#!/bin/zsh

## If in git repo : Show status ####
#
# All of this is taken from Sudish Joseph
# https://github.com/sudish/dotfiles/blob/master/.zsh.d/init.d/S90_git_status
# Blame him.
#
# Computes and displays the status of a git repo in PS1/RPS1.  Information
# displayed includes: branch, '*' for un-added changes, '+' for uncommitted
# changes, '$' for stashed changes.  In addition, the color of the status
# display changes from green to blue if you're in a git svn repo.
#
#   setopt prompt_subst
#   RPROMPT+='$(sj_git_ps1)'
#
# Inspired by and adapted from http://www.jukie.net/~bart/conf/zsh.d/S55_git
# and Shawn O. Pearce's completion script for bash:
# http://repo.or.cz/w/git.git?a=blob_plain;f=contrib/completion/git-completion.bash;hb=master


autoload -Uz add-zsh-hook

# Create an array called jb_git & initialize it
typeset -A jb_git
jb_git=()

jb_git_status_update() {
		local git_dir branch
		
		[[ -n $JB_DEBUG ]] && echo "Parsing git state" >/dev/tty

		jb_git[dir]=
		git_dir=$(git rev-parse --git-dir 2>/dev/null) || return

		jb_git[state]=
    if [[ -d $git_dir/rebase-apply ]]; then
        if [[ -f $git_dir/rebase-apply/rebasing ]]; then
            jb_git[state]="|REBASE"
        elif [[ -f $git_dir/rebase-apply/applying ]]; then
            jb_git[state]="|AM"
        else
            jb_git[state]="|AM/REBASE"
        fi
        branch="$(git symbolic-ref HEAD 2>/dev/null)"
    elif [[ -f $git_dir/rebase-merge/interactive ]]; then
        jb_git[state]="|REBASE -i"
        branch="$(cat "$git_dir/rebase-merge/head-name")"
    elif [[ -d $git_dir/rebase-merge ]]; then
        jb_git[state]="|REBASE -m"
        branch="$(cat "$git_dir/rebase-merge/head-name")"
    elif [[ -f $git_dir/MERGE_HEAD ]]; then
        jb_git[state]="|MERGING"
        branch="$(git symbolic-ref HEAD 2>/dev/null)"
    else
        if [[ -f $git_dir/BISECT_LOG ]]; then
						jb_git[state]="|BISECTING"
				fi
        branch=$(git symbolic-ref HEAD 2>/dev/null) || \
						branch=$(git describe --exact-match HEAD 2>/dev/null) || \
						branch="$(cut -c1-7 "$git_dir/HEAD")..."
    fi
		
		jb_git[stash]=
		if [[ -e $git_dir/refs/stash ]]; then
				jb_git[stash]='$'
		fi
		
		jb_git[svn]=
		if [[ -e $git_dir/svn/.metadata ]]; then
				jb_git[svn]='S'
		fi

		jb_git[status]=
		git diff --no-ext-diff --ignore-submodules --quiet --exit-code \
				|| jb_git[status]+='*'
		if git rev-parse --quiet --verify HEAD >/dev/null; then
				git diff-index --cached --quiet --ignore-submodules HEAD -- \
						|| jb_git[status]+='+'
		else
				jb_git[status]+='#'
		fi

		local git_full_dir
		git_full_dir=${git_dir:h}

    # "" quotes needed here to stop auto_name_dirs.
    # though why "" stops it from happening is a mystery, albeit a nice one
    jb_git[dir]="${git_full_dir/$HOME/~}"
    jb_git[branch]=${branch#refs/heads/}
}

jb_git_ps1() {
    if [[ -z $jb_git[dir] ]]; then
				echo "$vcs_info_msg_0_"
				return
		fi

    local col
    col="$cb[green]"
    [[ -n $jb_git[svn] ]] && col="$cb[cyan]"

		local res
    # the branch name
		res="$col${jb_git[branch]}$cb[none]"

		# Are there changes not in the index?
		[[ -n $jb_git[status] ]] && res+="$cb[magenta]${jb_git[status]}$cb[none]"

		# Does git stash contain changes?
		[[ -n $jb_git[stash] ]] && res+="$cb[magenta]${jb_git[stash]}$cb[none]"

		# What is the current state of the repo?
		[[ -n $jb_git[state] ]] && res+="$cb[red]${jb_git[state]}$cb[none]"

		echo " %Bgit%b:$res"
}

jb_git_chpwd_update() {
		jb_git_status_update
}

jb_git_precmd_update() {
    case $(history $(($HISTCMD - 1)) ) in   # last command entered
        *git*) jb_git_status_update ;;
    esac
}

add-zsh-hook precmd jb_git_precmd_update
add-zsh-hook chpwd jb_git_chpwd_update
